// Функция - Сериализовать объект
//
// Параметры:
//  Формат - Перечисление.сшпФорматыСообщений - текущий формат сообщения.
//  ОбъектИсточник - ЛюбаяСсылка, ЛюбойОбъект	 - объект который необходимо сериализовать в указанный формат.
// 
// Возвращаемое значение:
//  Строка - данные объекта преобразованные в указанный формат. 
//
Функция СериализоватьОбъект(Формат, ОбъектИсточник) Экспорт
		
	ТекОбъект = Неопределено;
	
	Попытка
	
		Если Формат = Перечисления.сшпФорматыСообщений.XML Тогда
	
			ТекОбъект = СериализоватьОбъектXML(ОбъектИсточник);
		
		ИначеЕсли Формат = Перечисления.сшпФорматыСообщений.JSON Тогда
		
			ТекОбъект = СериализоватьОбъектJSON(ОбъектИсточник);
		
		ИначеЕсли Формат = Перечисления.сшпФорматыСообщений.FastInfoset Тогда
		
			ТекОбъект = СериализоватьОбъектFI(ОбъектИсточник);
		
		КонецЕсли;
	
	Исключение
	
		ЗаписьЖурналаРегистрации("Datareon. Сериализация объекта", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));		
	
	КонецПопытки;
	
	Возврат ТекОбъект;

КонецФункции

// Функция - Сериализовать объект
//
// Параметры:
//	Источник - объект, который необходимо сериализовать.
// 
// Возвращаемое значение:
//	Результат сериализации объекта.	
//
Функция СериализоватьОбъектXML(Источник)
	
	текОбъектЗаписи = ПолучитьОбъектПотоковойЗаписиXML();
	СериализаторXDTO.ЗаписатьXML(текОбъектЗаписи, Источник, НазначениеТипаXML.Явное);
	
	Возврат текОбъектЗаписи.Закрыть();

КонецФункции

// Функция - Сериализовать объект
//
// Параметры:
//	Источник - объект, который необходимо сериализовать.
// 
// Возвращаемое значение:
//	Результат сериализации объекта.	
//
Функция СериализоватьОбъектJSON(Источник)
	
	ТекОбъектЗаписи = ПолучитьОбъектПотоковойЗаписиJSON();
	СериализаторXDTO.ЗаписатьJSON(текОбъектЗаписи, Источник, НазначениеТипаXML.Явное);
	
	Возврат текОбъектЗаписи.Закрыть();
	
КонецФункции

// Функция - Сериализовать объект
//
// Параметры:
//	Источник - объект, который необходимо сериализовать.
// 
// Возвращаемое значение:
//	Результат сериализации объекта.	
//
Функция СериализоватьОбъектFI(Источник)
	
	ТекОбъектЗаписи = ПолучитьОбъектПотоковойЗаписиFI();
	СериализаторXDTO.ЗаписатьXML(ТекОбъектЗаписи, Источник, НазначениеТипаXML.Явное);
	
	Возврат текОбъектЗаписи.Закрыть();
	
КонецФункции

// Функция - Получить объект потоковой записи
//
// Параметры:
//	ПараметрыЗаписи - входящие параметры записи XML.
//
// Возвращаемое значение:
//	ЗаписьXML - объект записи XML данных.
//
Функция ПолучитьОбъектПотоковойЗаписиJSON(ПараметрыЗаписи = Неопределено)
	
	ТекЗапись = Новый ЗаписьJSON;
	
	Если ПараметрыЗаписи = Неопределено Тогда
		
		ПараметрыЗаписи = ТекЗапись.Параметры;
	
	КонецЕсли;	
	
	ТекЗапись.УстановитьСтроку(ПараметрыЗаписи);
	
	Возврат ТекЗапись;

КонецФункции

// Функция - Получить объект потоковой записи
//
// Параметры:
//	Кодировка - кодировка, в которой необходимо произвести запись данных.
//
// Возвращаемое значение:
//	ЗаписьXML - объект записи XML данных.
//
Функция ПолучитьОбъектПотоковойЗаписиXML(Кодировка = "")
	
	текЗапись = Новый ЗаписьXML;
	текЗапись.УстановитьСтроку(Кодировка);
	
	Возврат текЗапись;
	
КонецФункции

// Функция - Получить объект потоковой записи
// 
// Возвращаемое значение:
//	ЗаписьFastInfoset - объект записи XML данных.
//
Функция ПолучитьОбъектПотоковойЗаписиFI()
	
	ТекЗапись = Новый ЗаписьFastInfoset;
	ТекЗапись.УстановитьДвоичныеДанные();
	
	Возврат текЗапись;
	
КонецФункции
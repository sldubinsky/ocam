#Область ПрограммныйИнтерфейс

Функция ПолучитьДанныеОбъектаПоСсылкеНаЭлемент(ЗНАЧ ОписаниеЗапроса) Экспорт
	
	Результат = ?(ОписаниеЗапроса.ВернутьМассивСтруктур, Новый Массив, Новый ТаблицаЗначений);
	
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(ОписаниеЗапроса.Элемент)) Тогда		
		ОбъектРодитель = "Справочник";
		Объект = ОписаниеЗапроса.Элемент.Метаданные().Имя;
		Объект = ?(ОписаниеЗапроса.ТабличнаяЧасть = Неопределено, Объект, Объект + "." + ОписаниеЗапроса.ТабличнаяЧасть);
	Иначе
		ВызватьИсключение Нстр("ru = 'Тип объекта не поддерживается'");
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	%4
	                      |ИЗ
	                      |	%1.%2 КАК Д
						  |%3");
	
	Реквизиты = СтрСоединить(ОписаниеЗапроса.Реквизиты, "," + Символы.ПС + Символы.Таб);
	Реквизиты = ?(ПустаяСтрока(Реквизиты), "*", Реквизиты);
	
	Условие = "";
	ПервоеУсловие = Истина;
	Для Каждого Стр Из ОписаниеЗапроса.Условие Цикл
		
		Если Стр.ВидСравнения = "=" Тогда
			СтрокаУсловия = "Д.%1 = &%1";
		ИначеЕсли Стр.ВидСравнения = "В" Тогда
			СтрокаУсловия = "Д.%1 В (&%1)";
		Иначе
			Продолжить;
		КонецЕсли;

		Условие = Условие + ?(ПервоеУсловие, "ГДЕ" + Символы.ПС, Символы.ПС + Символы.Таб + "И ");
		Условие = Условие + ?(Стр.НеСодержит, "НЕ ", "");
		Условие = Условие + СтрШаблон(СтрокаУсловия, Стр.ЛевоеЗначение);
		Запрос.УстановитьПараметр(Стр.ЛевоеЗначение, Стр.ПравоеЗначение);
		ПервоеУсловие = Ложь;
		
	КонецЦикла;
	
	Запрос.Текст = СтрШаблон(Запрос.Текст, ОбъектРодитель, Объект, Условие, Реквизиты);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ОписаниеЗапроса.ВернутьМассивСтруктур Тогда
		Результат = ОбработатьРезультатЗапроса(РезультатЗапроса);		
	Иначе
		Результат = РезультатЗапроса.Выгрузить();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьОписаниеЗапроса() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Элемент");
	Результат.Вставить("ВернутьМассивСтруктур", Ложь);
	Результат.Вставить("Условие", Новый Массив);
	Результат.Вставить("Реквизиты", Новый Массив);
	Результат.Вставить("ТабличнаяЧасть");
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьОписаниеУсловия() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ЛевоеЗначение");
	Результат.Вставить("ВидСравнения", "=");
	Результат.Вставить("ПравоеЗначение");
	Результат.Вставить("НеСодержит", Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция ОбработатьРезультатЗапроса(РезультатЗапроса) Экспорт

	Результат = Новый Массив;
	Колонки = Новый Массив;
	КолонкиСПодзапросом = Новый Массив;
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл		
		Если Колонка.ТипЗначения.СодержитТип(Тип("РезультатЗапроса")) Тогда
			КолонкиСПодзапросом.Добавить(Колонка.Имя);
		Иначе
			Колонки.Добавить(Колонка.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = ПолучитьСтруктуруИзСпискаКолонок(Колонки);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Для Каждого Колонка Из КолонкиСПодзапросом Цикл
			НоваяСтрока.Вставить(Колонка, ОбработатьРезультатЗапроса(Выборка[Колонка]));
		КонецЦикла;
		Результат.Добавить(НоваяСтрока);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Функция ПолучитьСтруктуруИзСпискаКолонок(СписокКолонок) Экспорт

	Результат = Новый Структура;

	Для Каждого Колонка Из СписокКолонок Цикл
		Результат.Вставить(Колонка);
	КонецЦикла;
	
	Возврат Результат;	

КонецФункции

#КонецОбласти
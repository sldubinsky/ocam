#Область ИсполнениеМетодовОбщихМодулей_GET
Функция ИсполнениеМетодовОбщихМодулей_GET(Запрос)
	Перем ВозвращаемыеДанные;
	
	Модуль = Запрос.ПараметрыURL.Получить("module");
	Метод = Запрос.ПараметрыURL.Получить("method");
	Параметры = Запрос.ПараметрыЗапроса;
	
	Если Модуль = Неопределено Или Метод = Неопределено Тогда
		Возврат Новый HTTPСервисОтвет(400);
	КонецЕсли;
	
	Шаблоны = ПолучитьШаблоныОбщихМодулей();
	ПараметрыОтбора = Новый Структура("module, method", Модуль, Метод);
	Отбор = Шаблоны.НайтиСтроки(ПараметрыОтбора);
	
	Если Отбор.Количество() = 0 Тогда
		Возврат Новый HTTPСервисОтвет(404);
	КонецЕсли;
	
	Для Каждого СтрокаОтбора Из Отбор Цикл
		Попытка
			СтрПараметры = ?(СтрокаОтбора.ИспользоватьПараметры, "Параметры", "");
			// BSLLS:ExecuteExternalCode-off
			Выполнить СтрШаблон("ВозвращаемыеДанные = %1.%2(%3)", СтрокаОтбора.ОбщийМодуль, СтрокаОтбора.Метод, СтрПараметры);
			// BSLLS:ExecuteExternalCode-on
		Исключение
			Код = СтрШаблон("ВозвращаемыеДанные = %1.%2(%3)", СтрокаОтбора.ОбщийМодуль, СтрокаОтбора.Метод, СтрПараметры);
			Ошибка = "Ошибка при попытке выполнить код: " + Код + Символы.ПС + Символы.ПС + Символы.ПС;
			Ошибка = Ошибка + ОписаниеОшибки();
			Ответ = Новый HTTPСервисОтвет(500);
			Ответ.УстановитьТелоИзСтроки(Ошибка);
			Возврат Ответ;
		КонецПопытки;
		Прервать
	КонецЦикла;
	
	Ответ = Новый HTTPСервисОтвет(200);
	Если ВозвращаемыеДанные <> Неопределено Тогда
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON, ВозвращаемыеДанные);
		Ответ.УстановитьТелоИзСтроки(ЗаписьJSON.Закрыть(), "UTF-8", ИспользованиеByteOrderMark.НеИспользовать);
	КонецЕсли;
	
	Возврат Ответ;
КонецФункции

Функция ПолучитьШаблоныОбщихМодулей()	
	Шаблоны = Новый ТаблицаЗначений;
	
	Шаблоны.Колонки.Добавить("ОбщийМодуль");
	Шаблоны.Колонки.Добавить("Метод");
	Шаблоны.Колонки.Добавить("ИспользоватьПараметры");
	Шаблоны.Колонки.Добавить("module");
	Шаблоны.Колонки.Добавить("method");
	
	АдминистративныйАПИПереопределяемый.ДобавитьШаблонОбщегоМодуля(Шаблоны);
	
	Возврат Шаблоны;
КонецФункции

#КонецОбласти

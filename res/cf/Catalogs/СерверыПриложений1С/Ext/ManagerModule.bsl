
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПортКластера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Данные.Ссылка, "ПортКластера");
	ПортКластера = ?(ПортКластера=0, 1541, ПортКластера);
	Представление = Данные.Наименование + ":" + Формат(ПортКластера, "ЧДЦ=0; ЧГ=0");
	
КонецПроцедуры

Функция ПолучитьПараметрыСервера(Сервер) Экспорт
	
	ПараметрыАдминистрирования = АдминистрированиеКластера.ПараметрыАдминистрированияКластера();
	ПараметрыАдминистрирования.АдресСервераАдминистрирования = Сервер.Наименование;
	ПараметрыАдминистрирования.ПортКластера = Сервер.ПортКластера;
	ПараметрыАдминистрирования.ПортСервераАдминистрирования = Сервер.ПортСервераАдминистрирования;
	ПараметрыАдминистрирования.ИмяАдминистратораКластера = Сервер.АвторизационныеДанные.Логин;
	ПараметрыАдминистрирования.ПарольАдминистратораКластера = Сервер.АвторизационныеДанные.Пароль;
	ПараметрыАдминистрирования.ТипПодключения = "RAS";
	
	Возврат ПараметрыАдминистрирования;
	
КонецФункции

Процедура ЗаполнитьПортСервераАдминистрирования() Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	СерверыПриложений1С.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.СерверыПриложений1С КАК СерверыПриложений1С
	               |ГДЕ
	               |	СерверыПриложений1С.ПортСервераАдминистрирования = 0";
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектЗаписи = Выборка.Ссылка.ПолучитьОбъект();
		ОбъектЗаписи.ПортСервераАдминистрирования = ОбъектЗаписи.ПортКластера + 4;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОбъектЗаписи);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСписокДляВыбора(Проект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Проект) Тогда
		ВызватьИсключение "Укажите проект!";
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	СерверыПриложений1С.Ссылка КАК Ссылка,
	               |	СерверыПриложений1С.Представление КАК Представление
	               |ИЗ
	               |	Справочник.СерверыПриложений1С КАК СерверыПриложений1С
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты.РазрешенныеСерверы КАК ПроектыРазрешенныеСерверы
	               |		ПО СерверыПриложений1С.Сервер = ПроектыРазрешенныеСерверы.Сервер
	               |ГДЕ
	               |	ПроектыРазрешенныеСерверы.Ссылка = &Проект";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Результат = Новый СписокЗначений();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Ссылка, Выборка.Представление);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции // ПолучитьСписокДляВыбора()

Функция ПолучитьИмяДляСтрокиСоединения(Сервер) Экспорт

	ТекстЗапроса = "ВЫБРАТЬ
	               |	СерверыПриложений1С.Представление КАК ИмяСервера
	               |ИЗ
	               |	Справочник.СерверыПриложений1С КАК СерверыПриложений1С
	               |ГДЕ
	               |	СерверыПриложений1С.Ссылка = &Ссылка
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СерверыПриложений1СДополнительныеСервера.СерверПриложений.Представление
	               |ИЗ
	               |	Справочник.СерверыПриложений1С.ДополнительныеСервера КАК СерверыПриложений1СДополнительныеСервера
	               |ГДЕ
	               |	СерверыПриложений1СДополнительныеСервера.Ссылка = &Ссылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Сервер);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	КонецЕсли;
	
	ТаблицаВыгрузка = Запрос.Выполнить().Выгрузить();
	Возврат СтрСоединить(ТаблицаВыгрузка.ВыгрузитьКолонку("ИмяСервера"), ",");

КонецФункции // ПолучитьИмяДляСтрокиСоединения()

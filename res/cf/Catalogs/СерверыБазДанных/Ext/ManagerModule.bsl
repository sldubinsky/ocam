
Функция ПолучитьПараметрыСервера(Сервер) Экспорт
	
	ДанныеСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Сервер, "Наименование,ТипСервераБД,АвторизационныеДанные");
	ПараметрыСервера = ПараметрыСервераБД();
	ПараметрыСервера.ИмяСервера = ДанныеСсылки.Наименование;
	ПараметрыСервера.ТипСервера = ПолучитьПредставлениеТипаСервераБД(ДанныеСсылки.ТипСервераБД);
	Если ЗначениеЗаполнено(ДанныеСсылки.АвторизационныеДанные) Тогда
		ПараметрыСервера.АдминистраторБД = ДанныеСсылки.АвторизационныеДанные.Логин;
		ПараметрыСервера.ПарольБД = ДанныеСсылки.АвторизационныеДанные.Пароль;
	КонецЕсли;
	
	Возврат ПараметрыСервера;
	
КонецФункции

Функция ПараметрыСервераБД()

	ПараметрыСервера = Новый Структура;
	ПараметрыСервера.Вставить("ИмяСервера", "");
	ПараметрыСервера.Вставить("ТипСервера", "");
	ПараметрыСервера.Вставить("АдминистраторБД", "");
	ПараметрыСервера.Вставить("ПарольБД", "");
	
	Возврат ПараметрыСервера;

КонецФункции // ПараметрыСервераБД()

Функция ПолучитьПредставлениеТипаСервераБД(ТипСервера)

	Если ТипСервера = Перечисления.ТипыСерверовБД.MSSQL Тогда
		Возврат "MSSQLServer";
	ИначеЕсли ТипСервера = Перечисления.ТипыСерверовБД.PostgreSQL Тогда
		Возврат "PostgreSQL";
	Иначе
		ВызватьИсключение "Неизвестный тип сервера БД!";
	КонецЕсли;

КонецФункции // ПолучитьПредставлениеТипаСервераБД()

Функция ПолучитьСписокДляВыбора(Проект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Проект) Тогда
		ВызватьИсключение "Укажите проект!";
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	СерверыБазДанных.Ссылка КАК Ссылка,
	               |	СерверыБазДанных.Представление КАК Представление
	               |ИЗ
	               |	Справочник.СерверыБазДанных КАК СерверыБазДанных
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты.РазрешенныеСерверы КАК ПроектыРазрешенныеСерверы
	               |		ПО СерверыБазДанных.Сервер = ПроектыРазрешенныеСерверы.Сервер
	               |ГДЕ
	               |	ПроектыРазрешенныеСерверы.Ссылка = &Проект";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Проект", Проект);
	
	Результат = Новый СписокЗначений();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Ссылка, Выборка.Представление);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции // ПолучитьСписокДляВыбора()
